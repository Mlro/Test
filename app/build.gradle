apply plugin: 'com.android.application'

android {
    compileSdkVersion 20
    buildToolsVersion "20.0.0"

    defaultConfig {
        applicationId "ro.mole.ro.test"
        minSdkVersion 10
        targetSdkVersion 20
        versionCode 1
        versionName "1.0"

        versionCode = 123 // must be between [0, 99999]

        ndk {
            moduleName "MyLib"
            stl "stlport_static"
            ldLibs "log"
        }
    }

    // 2 dimensions of flavors. API is more important than ABI.
    flavorGroups "abi"

    productFlavors {
        x86 {
            flavorGroup "abi"
            ndk {
                abiFilter "x86"
                cFlags "-mtune=atom -mssse3 -mfpmath=sse"
            }
            // this is the flavor part of the version code.
            // It must be higher than the arm one for devices supporting
            // both, as x86 is preferred.
            versionCode = 3
        }
        arm {
            flavorGroup "abi"
            ndk {
                abiFilter "armeabi-v7a"
            }
            versionCode = 2
        }
        mips {
            flavorGroup "abi"
            ndk {
                abiFilter "mips"
            }
            versionCode = 1
        }
        fat {
            flavorGroup "abi"
            // fat binary, lowest version code to be
            // the last option
            versionCode = 0
        }
    }

    // make per-variant version code
    applicationVariants.all { variant ->
        // get the version code of each flavor
        def abiVersion = variant.productFlavors.get(0).versionCode

        // set the composite code
        variant.mergedFlavor.versionCode = abiVersion * 1000000 + defaultConfig.versionCode
    }
    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile 'com.actionbarsherlock:actionbarsherlock:4.4.0@aar'
    compile files('libs/android-support-v4.jar')
    compile project(':android-support-v4-preferencefragment-master')
    compile project(':dialogs-Master')
    compile project(':nineoldandroids')
    compile project(':library')

}
